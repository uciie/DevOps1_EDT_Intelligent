plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    plantuml
}

repositories {
    mavenCentral()
    maven { url = "https://jitpack.io" } //pour spring-dotenv
    maven { url = "https://repo1.maven.org/maven2" } //pour biweekly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    
    // C'est cette dépendance qui permet le Chatbot (via RestClient) et Google Maps
    implementation 'org.springframework.boot:spring-boot-starter-web' //Pour les appels REST vers Google Maps API et Google AI (Gemini)

    implementation 'me.paulschwarz:spring-dotenv:4.0.0'
    //implementation 'com.fasterxml.jackson.core:jackson-annotations' // Inclus par défaut dans starter-web
    implementation 'com.github.mangstadt:biweekly:0.6.8' //parser

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    
    runtimeOnly 'com.h2database:h2'
    // Driver PostgreSQL pour la connexion à la base de données configurée dans .env
    runtimeOnly 'org.postgresql:postgresql'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Dépendances pour la génération de diagrammes UML
    plantuml 'net.sourceforge.plantuml:plantuml:1.2023.13'
}

tasks.named('test') {
    useJUnitPlatform()
    // Avoid Spring Boot rejecting legacy SPRING_PROFILES env var during tests
    // Remove SPRING_PROFILES from the environment passed to the forked test JVM
    environment.remove('SPRING_PROFILES')
    
    // Configuration pour charger les variables d'environnement depuis .env lors des tests
    doFirst {
        // ensure SPRING_PROFILES is not present in the environment passed to the forked test JVM
        environment.remove('SPRING_PROFILES')

        def envFile = file('.env')
        if (envFile.exists()) {
            envFile.eachLine { line ->
                line = line.trim()
                if (line && !line.startsWith('#')) {
                    def parts = line.split('=', 2)
                    if (parts.length == 2) {
                        def key = parts[0].trim()
                        def value = parts[1].trim()
                        // On évite d'écraser des variables système critiques
                        // and avoid passing deprecated Spring property names coming from .env
                        if (key.equalsIgnoreCase('SPRING_PROFILES') || key.equalsIgnoreCase('spring.profiles')) {
                            // skip legacy spring.profiles entries to prevent InvalidConfigDataPropertyException
                        } else if (key != 'PATH' && key != 'JAVA_HOME') {
                            environment key, value
                        }
                    }
                }
            }
            println ">>> .env chargé avec succès pour la tâche test"
        } else {
            println ">>> ATTENTION : Aucun fichier .env trouvé pour les tests"
        }
    }
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.60 // 60% de couverture minimale
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

// Configuration de SonarQube
sonarqube {
    properties {
        property "sonar.projectKey", "uciie_DevOps1_EDT_Intelligent"
        property "sonar.organization", "uciie"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

task generateUML(type: JavaExec) {
    group = "documentation"
    description = "Génère les diagrammes UML à partir des fichiers .puml"

    classpath = configurations.plantuml
    mainClass = "net.sourceforge.plantuml.Run"
    
    // Arguments : dossier source et format
    args "-tpng", "backend/output", "-o", "backend/output/images"
}
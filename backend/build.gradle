plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    plantuml
}

repositories {
	mavenCentral()
	maven { url = "https://jitpack.io" } //pour spring-dotenv			  
	maven { url = "https://repo1.maven.org/maven2" } //pour biweekly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'//Pour les appels REST vers Google Maps API
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'
	//implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.github.mangstadt:biweekly:0.6.8' //parser
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	testRuntimeOnly 'com.h2database:h2'  // H2 uniquement pour les tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.mockito:mockito-core:5.6.0' // Mockito
    testImplementation 'org.mockito:mockito-junit-jupiter:5.6.0' // Mockito avec JUnit5
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    plantuml 'net.sourceforge.plantuml:plantuml:1.2024.7'
    implementation 'org.postgresql:postgresql'
    // SDK Google AI Studio pour Gemini
    implementation 'com.google.genai:google-genai:1.17.0' 
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.withType(org.springframework.boot.gradle.tasks.bundling.BootJar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

// Configuration de JaCoCo
jacoco {
    // Upgrade JaCoCo to support newer JDK class file versions (Java 21 / major version 68)
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.70 // 70% de couverture minimale
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

// Configuration de SonarQube
sonarqube {
    properties {
        property "sonar.projectKey", "uciie_DevOps1_EDT_Intelligent"
        property "sonar.organization", "uciie"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

task generateUML(type: JavaExec) {
    group = "documentation"
    description = "Génère les diagrammes UML à partir des fichiers .puml"

    classpath = configurations.plantuml
    mainClass = "net.sourceforge.plantuml.Run"
    
    // Arguments : dossier source et format
    args = ["-tpng", "-o", "../uml", "../uml/*.puml"]
    
    // Optionnel : permet de ne pas échouer si le dossier est vide
    doFirst {
        file("../uml").mkdirs()
    }
}

build.dependsOn generateUML
// ./gradlew generateUML pour générer manuellement les diagrammes UML